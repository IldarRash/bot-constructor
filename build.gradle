plugins {
    id "org.springframework.boot" version "2.5.6" apply false
    id "io.spring.dependency-management" version "1.0.11.RELEASE" apply false
    id "com.github.node-gradle.node" version "3.0.1" apply false
    id "org.jetbrains.kotlin.jvm" version "1.5.31" apply false
    id "org.jetbrains.kotlin.plugin.spring" version "1.5.31" apply false
}

subprojects {
    apply plugin: "java"
    apply plugin: "kotlin"
    apply plugin: "kotlin-spring"
    apply plugin: "kotlin-kapt"
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"

    repositories {
        mavenCentral()
        maven { url 'https://oss.jfrog.org/artifactory/oss-snapshot-local' }
        maven { url 'https://repo.spring.io/milestone' }
        maven { url 'https://repo.spring.io/snapshot' }
    }

    dependencyManagement {
        dependencies {
            dependency("io.micrometer.prometheus:prometheus-rsocket-spring:1.3.0")
        }
    }
    dependencies {

        implementation("com.fasterxml.jackson.module:jackson-module-kotlin")
        implementation("io.projectreactor.kotlin:reactor-kotlin-extensions")
        implementation("org.jetbrains.kotlin:kotlin-reflect")
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
        implementation("org.jetbrains.kotlinx:kotlinx-coroutines-reactor")

        implementation("org.springframework.boot:spring-boot-starter-actuator")
        implementation("org.springframework.boot:spring-boot-starter-rsocket")
        implementation("io.projectreactor.addons:reactor-extra")
        implementation("org.springframework.boot:spring-boot-starter-data-mongodb-reactive")

        [
                enforcedPlatform('org.springframework.boot:spring-boot-dependencies:2.5.5'),
                platform('org.junit:junit-bom:5.4.0'),
                platform('io.projectreactor:reactor-bom:2020.0.11'),
                platform('io.github.resilience4j:resilience4j-bom:1.7.0'),
        ].each {
            implementation it
            annotationProcessor it
            testAnnotationProcessor it
        }
    }

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs += "-Xjsr305=strict"
            jvmTarget = "11"
        }
    }

    test {
        useJUnitPlatform()
    }
}

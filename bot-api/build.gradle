import io.netifi.flatbuffers.plugin.tasks.FlatBuffers

plugins {
    id "org.springframework.boot"
    id "io.spring.dependency-management"
    id "io.netifi.flatbuffers"
    id "org.jetbrains.kotlin.jvm"
    id "org.jetbrains.kotlin.plugin.spring"
}

sourceCompatibility = JavaVersion.VERSION_11

configurations {
    flatbuf
}

repositories {
    maven { url "https://jitpack.io" }
}

dependencies {

    flatbuf(project(":bots-model-idl"))

    implementation("org.springframework.boot:spring-boot-starter-webflux")
    implementation("org.springframework.boot:spring-boot-starter-rsocket")
    implementation("org.springframework.boot:spring-boot-starter-thymeleaf")

    implementation("com.fasterxml.jackson.module:jackson-module-kotlin")
    implementation("io.projectreactor.kotlin:reactor-kotlin-extensions")
    implementation("org.jetbrains.kotlin:kotlin-reflect")
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-reactor")

    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation("io.micrometer.prometheus:prometheus-rsocket-spring")

    implementation("org.thymeleaf:thymeleaf")
    implementation("org.thymeleaf:thymeleaf-spring5")

    annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
}

flatbuffers {
    flatBuffersVersion = '2.0.0'
}

processResources {
    dependsOn configurations.flatbuf
    configurations.flatbuf.files.each {
        from(zipTree(it).matching {
            include "*.fbs"
        })
    }
}

task createFlatBuffers(type: FlatBuffers, dependsOn: ['processResources']) {
    language = 'kotlin'
    inputDir = file("$buildDir/resources/main")
    outputDir = file("$buildDir/generated/sources/flatbuffers/java/main")
    extraArgs = "--gen-mutable"
}

compileKotlin {
    dependsOn createFlatBuffers
}
